apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'signing'


group = 'com.github.hippoom'
version = "0.1.0"


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {

    compile "org.springframework.boot:spring-boot-starter-web:${project.springBootVersion}"
    compile 'com.google.guava:guava:18.0'

    testCompile "org.springframework.boot:spring-boot-starter-test:${project.springBootVersion}"
    testCompile('junit:junit:4.12') {
        exclude group: 'org.hamcrest'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'com.jayway.restassured:rest-assured:2.4.0'
    testCompile 'com.jayway.jsonpath:json-path:0.9.1'
}

buildscript {

    ext {
        springBootVersion = '1.2.3.RELEASE'
        repositoryName = rootProject.name
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}")
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

test.finalizedBy jacocoTestReport

jar {
    archiveName "${rootProject.name}-${rootProject.version}.jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveName "${rootProject.name}-${rootProject.version}-javadoc.jar"
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    archiveName "${rootProject.name}-${rootProject.version}-sources.jar"
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

def publishStage() {
    project.hasProperty('publishStage')? publishStage: false
}

if (publishStage()) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    groupId rootProject.group
                    artifactId rootProject.name
                    version rootProject.version

                    name rootProject.name
                    packaging 'jar'
                    description "An extension to spring-mvc to simplify restful application stuff, see https://github.com/Hippoom/${repositoryName} for more detail."
                    url "https://github.com/Hippoom/${repositoryName}"

                    scm {
                        url "scm:git@github.com:Hippoom/${repositoryName}.git"
                        connection "scm:git@github.com:Hippoom/${repositoryName}.git"
                        developerConnection "scm:git@github.com:Hippoom/${repositoryName}.git"
                    }

                    licenses {
                        license {
                            name 'The Apache License 2.0'
                            url "https://github.com/Hippoom/${repositoryName}/blob/master/LICENSE"
                     //       Distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'hippoom'
                            name 'Yugang Zhou'
                        }
                    }
                }
            }
        }
    }
}
